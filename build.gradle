allprojects {
    group "org.hexworks.microcline"
    version "2018.0.1-PREVIEW"
}

buildscript {
    ext.kotlinVersion = "1.3.11"
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.2"
    }
}

ext {
    zirconVersion = "2019.0.10-PREVIEW"
    cobaltVersion = "2018.1.1-PREVIEW"
    junitVersion = "4.12"
    mockitoVersion = "1.10.19"
    assertjVersion = "3.6.2"
    slf4jVersion = "1.7.25"

    libraries = [
            kotlin_stdlib : "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
            kotlin_reflect: "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",
            slf4j_api     : "org.slf4j:slf4j-api:$slf4jVersion",
            slf4j_simple  : "org.slf4j:slf4j-simple:$slf4jVersion",
            zircon_core   : "org.hexworks.zircon:zircon.core-jvm:$zirconVersion",
            zircon_swing  : "org.hexworks.zircon:zircon.jvm.swing:$zirconVersion",
            cobalt        : "org.hexworks.cobalt:cobalt.databinding-jvm:$cobaltVersion",
            junit         : "junit:junit:$junitVersion",
            mockito       : "org.mockito:mockito-all:$mockitoVersion",
            assertj       : "org.assertj:assertj-core:$assertjVersion"
    ]
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

apply plugin: 'kotlin'
apply plugin: "com.github.johnrengelman.shadow"

configurations {
    shadow
    compile.extendsFrom provided
    provided.extendsFrom shadow
}

shadowJar {
    classifier = null
    version = version
}

artifacts {
    archives shadowJar
}

jar {
    manifest {
        attributes 'Main-Class': "org.hexworks.microcline.MainKt"
    }
}

build.dependsOn(shadowJar)

dependencies {
    compile libraries.kotlin_stdlib
    compile libraries.kotlin_reflect

    compile libraries.slf4j_api
    compile libraries.slf4j_simple

    compile libraries.zircon_core
    compile libraries.zircon_swing

    compile libraries.cobalt
    
    testCompile libraries.junit
    testCompile libraries.mockito
    testCompile libraries.assertj
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
